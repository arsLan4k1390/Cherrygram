import okhttp3.*

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter
import java.util.concurrent.TimeUnit

apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    mavenCentral()
    google()
    maven { url 'https://developer.huawei.com/repo/' }
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.configureEach {
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation project(':TMessagesProj')
    implementation 'androidx.fragment:fragment:1.8.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation files('../TMessagesProj/libs/libgsaverification-client.aar')

    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.huawei.hms:push:6.11.0.300'
    implementation 'com.huawei.hms:maps:6.11.0.304'
    implementation 'com.huawei.hms:location:6.11.0.301'
}

android {
    compileSdk 34
    buildToolsVersion = '34.0.0'

    defaultConfig.applicationId = APP_PACKAGE

    sourceSets.main.jniLibs.srcDirs = ['../TMessagesProj/jni/']

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'BlockedPrivateApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_14
        targetCompatibility JavaVersion.VERSION_14

        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../Your_Key.jks')
            storePassword 'Your_Password'
            keyAlias 'Your_Alias'
            keyPassword 'Your_Password'
        }

        release {
            storeFile file('../Your_Key.jks')
            storePassword 'Your_Password'
            keyAlias 'Your_Alias'
            keyPassword 'Your_Password'
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../TMessagesProj/proguard-rules.pro', '../TMessagesProj/proguard-rules-beta.pro'
            ndk.debugSymbolLevel = 'FULL'
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
        }
    }

    sourceSets.debug {
        manifest.srcFile '../TMessagesProj/config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile '../TMessagesProj/config/release/AndroidManifest.xml'
    }

    flavorDimensions = ["minApi"]

    productFlavors {
        afat {
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a", "arm64-v8a"/*, "x86", "x86_64"*/
            }
            sourceSets.debug {
                manifest.srcFile '../TMessagesProj/config/debug/AndroidManifest_SDK23.xml'
            }
            sourceSets.release {
                manifest.srcFile '../TMessagesProj/config/release/AndroidManifest_SDK23.xml'
            }
            ext {
                abiVersionCode = 9
            }
        }
    }

    defaultConfig.versionCode = Integer.parseInt(APP_VERSION_CODE)

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abi = output.getFilter('ABI')
            if (abi == null) abi = 'universal'
            outputFileName = "Cherrygram-Huawei-${APP_VERSION_NAME_CHERRY}-TG-${defaultConfig.versionName}-${abi}.apk"
            output.versionCodeOverride = defaultConfig.versionCode * 10
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("afat")) {
            setIgnore(true)
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionName APP_VERSION_NAME
        ndkVersion "21.4.7075529"

        multiDexEnabled true

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi']

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }
    namespace 'org.telegram.messenger.huawei'

    lintOptions {
        checkReleaseBuilds false
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    splits.abi {
        enable true
        reset()
        //noinspection ChromeOsAbiSupport
        include "arm64-v8a", "armeabi-v7a"
        universalApk true
    }
}

/** *
 *
 * CI/CD is used for sending APKs to Telegram and copying them to Desktop
 *
 * **/

static def isWindows() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('win')
}

static def isMacOS() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('mac')
}

static def isWindowsOrMacOS() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('win') || osName.contains('mac')
}

static def getOSName() {
    def buildHost
    if (isWindows()) {
        buildHost = 'Windows'
    } else if (isMacOS()) {
        buildHost = 'macOS'
    } else {
        buildHost = 'Unknown'
    }
    return buildHost
}

def desktopPath
if (isWindows()) {
    desktopPath = "C:/Users/${SYSTEM_USERNAME}/Desktop/Cherry/Stable"
} else if (isMacOS()) {
    desktopPath = "/Users/${SYSTEM_USERNAME}/Desktop/Cherry/Stable"
}

tasks.register('copyFiles', Copy) {
    from "${projectDir}/afat/release/"
    include '**/*.apk'
    exclude 'baselineProfiles/**'
    into desktopPath
}

def fileName = "Cherrygram-Huawei-${APP_VERSION_NAME_CHERRY}-TG-${APP_VERSION_NAME}-arm64-v8a.apk"
def apkFilePath = "${projectDir}/afat/release/${fileName}"

def fileName2 = "Cherrygram-Huawei-${APP_VERSION_NAME_CHERRY}-TG-${APP_VERSION_NAME}-armeabi-v7a.apk"
def apkFilePath2 = "${projectDir}/afat/release/${fileName2}"

tasks.register('sendApk') {
    doLast {
        def file = new File(apkFilePath)
        def file2 = new File(apkFilePath2)

        if (!file.exists() && !file2.exists()) {
            println "APK file not found: $apkFilePath"
            println "APK file not found: $apkFilePath2"
            return
        }

        println "Sending APK to Cherrygram Support..."

        try {
            sendApkToTelegram(file)
            sendApkToTelegram(file2)
        } catch (Exception e) {
            println "Error sending APK: ${e.message}"
            e.printStackTrace()
        }
    }
}

void sendApkToTelegram(File file) {
    OkHttpClient client = new OkHttpClient.Builder()
            .connectTimeout(90, TimeUnit.SECONDS)
            .build()


    def currentDateAndTime = LocalDateTime.now().format(DateTimeFormatter.ofPattern("dd.MM.yyyy, HH:mm"))
    def buildDate = "Build date: *" + currentDateAndTime + "*\n"

    def buildHost = "\nBuilt on: *${getOSName()}*"

    def currentRepo = 'git rev-parse --show-toplevel'.execute().text.trim().split('/').last()
    def currentBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    def projectName = "\n \nProject: *${currentRepo}*" + "/${currentBranch}"

    def lastCommitAuthor = 'git log -1 --pretty=%an'.execute().text.trim()
    def author = "\nTriggered by: *${lastCommitAuthor}*"


    def fileCaption
    if (file.name.contains("arm64-v8a")) {
        fileCaption = buildDate + "Architecture: *arm64-v8a*" + buildHost + projectName + author
    } else if (file.name.contains("armeabi-v7a")) {
        fileCaption = buildDate + "Architecture: *armeabi-v7a*" + buildHost + projectName + author
    } else {
        fileCaption = buildDate + "Architecture: *universal*" + buildHost + projectName + author
    }

    def requestBody = new MultipartBody.Builder()
            .setType(MultipartBody.FORM)
            .addFormDataPart("chat_id", TELEGRAM_CHAT_ID)
            .addFormDataPart("document", file.name,
                    RequestBody.create(MediaType.parse("application/vnd.android.package-archive"), file)
            )
            .addFormDataPart("caption", fileCaption)
            .addFormDataPart("parse_mode", "Markdown")
            .build()

    def request = new Request.Builder()
            .url("https://api.telegram.org/bot${TELEGRAM_BOT_TOKEN}/sendDocument")
            .post(requestBody)
            .build()

    println "Uploading file: ${file.name}, size: ${file.length()} bytes"

    client.newCall(request).execute().withCloseable { response ->
        if (response.isSuccessful()) {
            println "APK successfully sent to Cherrygram Support!"
        } else {
            throw new RuntimeException("HTTP ${response.code()} - ${response.message()}")
        }
    }
}

tasks.configureEach { task ->
    if (task.name == 'extractProguardFiles') {
        task.doFirst {
            delete "${projectDir}/afat/release/"
        }
    }

    if (task.name == 'assembleAfatRelease') {
        if (isWindowsOrMacOS()) {
            sendApk.mustRunAfter copyFiles
            task.finalizedBy 'copyFiles', 'sendApk'
        } else {
            task.finalizedBy 'sendApk'
        }
    }
}