import okhttp3.*

import java.nio.file.Files
import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter
import java.util.concurrent.TimeUnit

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'

/*repositories {
    mavenCentral()
    google()
}*/

configurations {
    compile.exclude module: 'support-v4'
}

configurations.configureEach {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation project(':TMessagesProj')
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation 'androidx.core:core-ktx:1.17.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation files('../TMessagesProj/libs/libgsaverification-client.aar')

    implementation 'com.aheaditec.talsec.security:TalsecSecurity-Community:17.0.0'
}

android {
    kotlinOptions {
        jvmTarget = '17'
    }

    compileSdk 36
    buildToolsVersion = '36.0.0'

    defaultConfig.applicationId = APP_PACKAGE

    sourceSets.main.jniLibs.srcDirs = ['../TMessagesProj/jni/']

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'BlockedPrivateApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17

        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../Your_Key.jks')
            storePassword 'Your_Password'
            keyAlias 'Your_Alias'
            keyPassword 'Your_Password'
        }

        release {
            storeFile file('../Your_Key.jks')
            storePassword 'Your_Password'
            keyAlias 'Your_Alias'
            keyPassword 'Your_Password'
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.release
//            applicationIdSuffix ".web"
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../TMessagesProj/proguard-rules.pro', '../TMessagesProj/proguard-rules-beta.pro'
            ndk.debugSymbolLevel = 'FULL'
        }
        standalone {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
//            applicationIdSuffix ".web"
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel = 'NONE'
        }
    }

    sourceSets.debug {
        manifest.srcFile '../TMessagesProj/config/release/AndroidManifest_standalone.xml'
    }
    sourceSets.standalone {
        manifest.srcFile '../TMessagesProj/config/release/AndroidManifest_standalone.xml'
    }

    flavorDimensions = ["minApi"]

    productFlavors {
        afat {
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a", "arm64-v8a"/*, "x86", "x86_64"*/
            }
            ext {
                abiVersionCode = 9
            }
            sourceSets.standalone {
                manifest.srcFile '../TMessagesProj/config/release/AndroidManifest_standalone.xml'
            }
        }
    }

    defaultConfig.versionCode = Integer.parseInt(APP_VERSION_CODE)

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abi = output.getFilter('ABI')
            if (abi == null) abi = 'universal'
            outputFileName = "Cherrygram-${APP_VERSION_NAME_CHERRY}-TG-${defaultConfig.versionName}-${abi}.apk"
            output.versionCodeOverride = defaultConfig.versionCode * 10
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("afat")) {
            setIgnore(true)
        }
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35
        versionName APP_VERSION_NAME
        ndkVersion "21.4.7075529"

        multiDexEnabled true

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi']

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21' //, '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON'
            }
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    buildFeatures {
        buildConfig = true
    }
    namespace 'org.telegram.messenger.web'

    lintOptions {
        checkReleaseBuilds false
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    splits.abi {
        enable true
        reset()
        //noinspection ChromeOsAbiSupport
        include "arm64-v8a", "armeabi-v7a"
        universalApk true
    }
}

/** *
 *
 * CI/CD is used for sending APKs to Telegram and copying them to Desktop
 *
 * **/

static def loadEnv(filePath) {
    def env = [:]
    Files.lines(Paths.get(filePath)).each { line ->
        if (line && !line.startsWith("#")) {
            def (key, value) = line.split('=', 2)
            env[key.trim()] = value?.trim()
        }
    }
    return env
}

def env = loadEnv("${project.rootDir}/secrets_for_ci.env")

env.each { key, value ->
    ext."$key" = value
}

/*task printSecrets {
    doLast {
        println "TELEGRAM_CHAT_ID: ${TELEGRAM_CHAT_ID}"
        println "TELEGRAM_BOT_TOKEN: ${TELEGRAM_BOT_TOKEN}"
        println "SYSTEM_USERNAME: ${SYSTEM_USERNAME}"
    }
}*/

static def isWindows() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('win')
}

static def isMacOS() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('mac')
}

static def isWindowsOrMacOS() {
    def osName = System.getProperty('os.name').toLowerCase()
    return osName.contains('win') || osName.contains('mac')
}

static def getOSName() {
    def buildHost
    if (isWindows()) {
        buildHost = 'Windows'
    } else if (isMacOS()) {
        buildHost = 'macOS'
    } else {
        buildHost = 'Unknown'
    }
    return buildHost
}

def desktopPath
if (isWindows()) {
    desktopPath = "C:/Users/${SYSTEM_USERNAME}/Desktop/Cherry/Stable"
} else if (isMacOS()) {
    desktopPath = "/Users/${SYSTEM_USERNAME}/Desktop/Cherry/Stable"
}

tasks.register('copyFiles', Copy) {
    from "${projectDir}/afat/standalone/"
    include '**/*.apk'
    exclude 'baselineProfiles/**'
    into desktopPath
}

def fileName = "Cherrygram-${APP_VERSION_NAME_CHERRY}-TG-${APP_VERSION_NAME}-arm64-v8a.apk"
def apkFilePath = "${projectDir}/afat/standalone/${fileName}"

def fileName2 = "Cherrygram-${APP_VERSION_NAME_CHERRY}-TG-${APP_VERSION_NAME}-armeabi-v7a.apk"
def apkFilePath2 = "${projectDir}/afat/standalone/${fileName2}"

tasks.register('sendApk') {
    doLast {
        def file = new File(apkFilePath)
        def file2 = new File(apkFilePath2)

        if (!file.exists() && !file2.exists()) {
            println "APK file not found: $apkFilePath"
            println "APK file not found: $apkFilePath2"
            return
        }

        println "Sending APK to Cherrygram Support..."

        try {
            sendApkToTelegram(file)
            sendApkToTelegram(file2)
        } catch (Exception e) {
            println "Error sending APK: ${e.message}"
            e.printStackTrace()
        }
    }
}

void sendApkToTelegram(File file) {
    OkHttpClient client = new OkHttpClient.Builder()
            .connectTimeout(90, TimeUnit.SECONDS)
            .build()

    def appVersion = "App version: *${APP_VERSION_NAME_CHERRY} // ${APP_VERSION_NAME} (${APP_VERSION_CODE})*"

    def currentDateAndTime = LocalDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ofPattern("dd.MM.yyyy, HH:mm"))
    def buildDate = "\n \nBuild date (UTC): *" + currentDateAndTime + "*\n"

    def buildHost = "\nBuilt on: *${getOSName()}*"

    def currentRepo = 'git rev-parse --show-toplevel'.execute().text.trim().split('/').last()
    def currentBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    def projectName = "\n \nProject: *${currentRepo}*" + "/${currentBranch}"

    def lastCommitAuthor = 'git log -1 --pretty=%an'.execute().text.trim()
    def author = "\nTriggered by: *${lastCommitAuthor}*"


    def fileCaption
    if (file.name.contains("arm64-v8a")) {
        fileCaption = appVersion + buildDate + "Architecture: *arm64-v8a*" + buildHost + projectName + author
    } else if (file.name.contains("armeabi-v7a")) {
        fileCaption = appVersion + buildDate + "Architecture: *armeabi-v7a*" + buildHost + projectName + author
    } else {
        fileCaption = appVersion + buildDate + "Architecture: *universal*" + buildHost + projectName + author
    }

    def requestBody = new MultipartBody.Builder()
            .setType(MultipartBody.FORM)
            .addFormDataPart("chat_id", "${TELEGRAM_CHAT_ID}")
            .addFormDataPart("document", file.name,
                    RequestBody.create(MediaType.parse("application/vnd.android.package-archive"), file)
            )
            .addFormDataPart("caption", fileCaption)
            .addFormDataPart("parse_mode", "Markdown")
            .build()

    def request = new Request.Builder()
            .url("https://api.telegram.org/bot${TELEGRAM_BOT_TOKEN}/sendDocument")
            .post(requestBody)
            .build()

    println "Uploading file: ${file.name}, size: ${file.length()} bytes"

    client.newCall(request).execute().withCloseable { response ->
        if (response.isSuccessful()) {
            println "APK successfully sent to Cherrygram Support!"
        } else {
            throw new RuntimeException("HTTP ${response.code()} - ${response.message()}")
        }
    }
}

tasks.configureEach { task ->
    if (task.name == 'extractProguardFiles') {
        task.doFirst {
            delete "${projectDir}/afat/standalone/"
        }
    }

    if (task.name == 'assembleAfatStandalone') {
        if (isWindowsOrMacOS()) {
            sendApk.mustRunAfter copyFiles
            task.finalizedBy 'copyFiles', 'sendApk'
        } else {
            task.finalizedBy 'sendApk'
        }
    }
}

apply plugin: 'com.google.gms.google-services'
